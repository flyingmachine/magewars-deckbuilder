(page "index.html"
  (:require [clojure.set :as s]
            [magewars-deckbuilder.deck :as d]
            [magewars-deckbuilder.filtering
             :as f :refer [filter-match filter-cards filter-options
                           filter-card card-index filter-vals fc2
                           attribute-filter-types empty-filters]]
            [tailrecursion.castra
             :as c :refer [mkremote]]))

(set! cljs.core/*print-fn* #(.log js/console %))

;; state
(def filter-attributes-ordered
  (reduce #(assoc % (second %2) (first %2))
          {}
          (map-indexed vector
                       [:type :subtypes :school :level
                        :targets :secondary-targets :range
                        :casting-cost :effects :traits
                        :speed :armor :life :channeling :defenses
                        :attack-dice :damage-types :ranged-melee :attack-speed
                        :slots])))
(defc filters (empty-filters attribute-filter-types))
(defc cards #{})
(defc error nil)
(def get-state (mkremote 'magewars-deckbuilder.api.deck/get-state cards error (cell nil)))
(defc= filtered-cards (filter-cards cards filters))
(defc= fcards (sort-by :name filtered-cards))
(defc= scards (set filtered-cards))
(defc= foptions (sort-by #(get filter-attributes-ordered (first %)) (filter-options cards)))
(defc= fvals (filter-vals cards))
(defc= cindex (card-index cards fvals))
(defc= counts
  (reduce merge
          (map (fn [[attr fvals]]
                 (let [cf (if (empty? (attr filters))
                            #(s/intersection scards (get cindex [attr %]))
                            #(fc2 cards (merge filters {attr #{%}}) cindex))]
                   {attr (reduce merge (map (fn [v] {v (count (cf v))}) fvals))}))
               fvals)))

;; 
(defn toggle-filter!
  [attr val]
  (fn []
    (let [f (if (get-in @filters [attr val]) disj conj)]
      (swap! filters #(merge-with f % {attr val})))))

(defn filter-count
  [counts attr val]
  (cell= (get-in counts [attr val])))

(defn vset?
  [attr]
  (= (attr attribute-filter-types) :vset))

(defn filter-li
  [attr val display]
  (li
   (label
    (input :type "checkbox"
           :on-change (toggle-filter! attr val))
    (text "~{display} ~{@(filter-count counts attr val)}"))))
(defn filter-val
  [attr val]
  (if (vset? attr)
    (let [[root leaves] val]
      ((filter-li attr [root] root)
       (if (> (count leaves) 0)
         (ul (map #(filter-li attr [root %] %) leaves)))))
    (filter-li attr val val)))

(defn filter-attribute
  [attr vals]
  (div (h3 (text "~{attr}"))
       (ul (map (partial filter-val attr) vals))))

(defn add-card
  [dest c]
  (let [count {:count 1}]
    (if-let [found (some #(and (= (:name %) (:name c)) %) dest)]
      (conj (disj dest found) (merge-with + found count))
      (conj dest (merge c count)))))
(defn remove-card
  [src c]
  (into (disj src c)
        (if (> (:count c) 1)
          [(merge-with - c {:count 1})])))

(defn move-card
  [card src dest]
  (fn []
    (let [c @card]
      (swap! src remove-card c)
      (swap! dest add-card c))))

(get-state)

(html
 (head
  (link :rel "stylesheet" :href "stylesheets/main.css"))
 (body
  (div :class "container"
   (h1 "Mage Wars Deck Builder")
   (div :class "filters"
    (h3 "Filters")
    (loop-tpl
     :size (count filter-attributes-ordered)
     :bindings [foption foptions]
     (filter-attribute (first @foption) (sort (second @foption)))))
   (div :class "cards"
    (h3 "Cards")
    (ul
     (loop-tpl :size 300 :bindings [card fcards]
               (li (text "~{(:name card)}"))))))))
