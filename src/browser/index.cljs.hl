(page "index.html"
  (:require [magewars-deckbuilder.deck :as d]
            [magewars-deckbuilder.filtering
             :as f :refer [filter-cards map-set-filter filter-options attribute-filter-types empty-filters]]
            [tailrecursion.castra
             :as c :refer [mkremote]]))

(set! cljs.core/*print-fn* #(.log js/console %))

(defn toggle-filter!
  [filters attr val]
  (fn []
    (let [f (if (get-in @filters [attr val]) disj conj)]
      (swap! filters #(merge-with f % {attr val})))))

(defn filter-count
  [all-cards filtered-cards filters attr val]
  (cell= (count
          (if (get-in filters [attr val])
            (filter #(= (attr %) val) filtered-cards)
            (filter
             #(and (= (attr %) val)
                   ((partial map-set-filter (dissoc filters attr)) %))
             all-cards)))))

(defn filter-val
  [all-cards cards filters attr val]
  (li
   (label
    (input :type "checkbox"
           :on-change (toggle-filter! filters attr val))
    (text "~{val} ~{@(filter-count all-cards cards filters attr val)}"))))
(defn filter-attribute
  [all-cards filtered-cards filters attr vals]
  (div (h3 (text "~{attr}"))
       (ul (map (partial filter-val all-cards filtered-cards filters attr)
                vals))))

(defn add-card
  [dest c]
  (let [count {:count 1}]
    (if-let [found (some #(and (= (:name %) (:name c)) %) dest)]
      (conj (disj dest found) (merge-with + found count))
      (conj dest (merge c count)))))
(defn remove-card
  [src c]
  (into (disj src c)
        (if (> (:count c) 1)
          [(merge-with - c {:count 1})])))

(defn move-card
  [card src dest]
  (fn []
    (let [c @card]
      (swap! src remove-card c)
      (swap! dest add-card c))))


(def filter-attributes-ordered
  [:type :subtypes :school :level
   :targets :secondary-targets :range
   :casting-cost :effects :traits
   :speed :armor :life :channeling :defenses
   :attack-dice :damage-types :ranged-melee :attack-speed
   :slot])

(defc filters (empty-filters attribute-filter-types))
(defc cards #{})
(defc error nil)

(def get-state (mkremote 'magewars-deckbuilder.api.deck/get-state cards error (cell nil)))
(defc= fcards (sort-by :name (filter-cards cards filters)))
(defc= foptions (filter-options cards))

(get-state)

(defn fc
  [vals]
  (div (text "~{vals}")))

(html
 (head)
 (body
   (h1 "Mage Wars Deck Builder")
   (div :id "deck"
     (h3 "Filters")
     
     (h3 "Cards")
     (ul
       (loop-tpl :size 300 :bindings [card fcards]
         (li (text "~{(:name card)}")))))))
